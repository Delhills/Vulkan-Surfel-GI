#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_GOOGLE_include_directive : enable

#include "helpers.glsl"

layout(binding = 0, set = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 1, set = 0, rgba8) uniform image2D shadowImage;
layout(binding = 2, set = 0) uniform CameraProperties 
{
	mat4 viewInverse;
	mat4 projInverse;
	float frame;
} cam;

struct shadowPayload{
	vec3 color;
	uint seed;
};

layout(location = 0) rayPayloadEXT shadowPayload prd;

void main() 
{
	float frame = cam.frame;
	prd.seed = tea(gl_LaunchIDEXT.y * gl_LaunchSizeEXT.x + gl_LaunchIDEXT.x, uint(frame));

	vec3 finalColor = vec3(0);

	for(int nS = 0; nS < NSAMPLES; nS++)
	{
		float r1 				= rnd(prd.seed);
		float r2 				= rnd(prd.seed);
		vec2 pixel_offset 		= nS == 0 ? vec2(0.5) : vec2(r1, r2);
		const vec2 pixelCenter 	= vec2(gl_LaunchIDEXT.xy) + pixel_offset;	// gl_LaunchIDEXT represents the floating-point pixel coordinates normalized between 0 and 1
		const vec2 inUV 		= pixelCenter/vec2(gl_LaunchSizeEXT.xy);	//gl_LaunchSizeExt is the image size provided in the traceRayEXT function
		vec2 d 					= inUV * 2.0 - 1.0;

		// Using the pixel coordinates we can apply the inverse transformation of the view and procjection matrices of the camera to obtain
		// the origin and target of the ray
		vec4 origin		= cam.viewInverse * vec4(0,0,0,1);
		vec4 target		= cam.projInverse * vec4(d.x, d.y, 1, 1) ;
		vec4 direction	= cam.viewInverse * vec4(normalize(target.xyz), 0);

		float tmin 		= 0.001;
		float tmax 		= 10000.0;

		// Call the function to start tracing rays.
		traceRayEXT(topLevelAS, gl_RayFlagsOpaqueEXT, 0xff, 0, 0, 0, origin.xyz + direction.xyz * 1e-2, tmin, direction.xyz, tmax, 0);
		finalColor += prd.color;
	}
	finalColor /= NSAMPLES;

	// Finally we can write the result to the output image
	//imageStore(shadowImage, ivec2(gl_LaunchIDEXT.xy), vec4(finalColor, 1.0));
	
	if(frame > 0)
	{
		float a = 1.0f / float(frame + 1);
		vec3 old_color = imageLoad(shadowImage, ivec2(gl_LaunchIDEXT.xy)).rgb;
		vec3 c = mix(old_color, finalColor, a);
		imageStore(shadowImage, ivec2(gl_LaunchIDEXT.xy), vec4(c, 1));
	}
	else{
		imageStore(shadowImage, ivec2(gl_LaunchIDEXT.xy), vec4(finalColor, 1.0));
	}
}
