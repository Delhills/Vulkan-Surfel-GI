#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_GOOGLE_include_directive : enable

#include "raycommon.glsl"
#include "helpers.glsl"

layout(binding = 0, set = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 1, set = 0, rgba8) uniform image2D image;
layout(binding = 2, set = 0) uniform CameraProperties 
{
	mat4 viewInverse;
	mat4 projInverse;
	int frame;
} cam;

layout(location = 0) rayPayloadEXT hitPayload prd;

void main() 
{
	const int frame = cam.frame;
	prd.seed = tea(gl_LaunchIDEXT.y * gl_LaunchSizeEXT.x + gl_LaunchIDEXT.x, frame);

	vec3 finalColor = vec3(0);

	for(int nS = 0; nS < NSAMPLES; nS++)
	{
		float r1 = rnd(prd.seed);
		float r2 = rnd(prd.seed);
		vec2 pixel_offset 		= nS == 0 ? vec2(0.5) : vec2(r1, r2);
		const vec2 pixelCenter 	= vec2(gl_LaunchIDEXT.xy) + pixel_offset;	// gl_LaunchIDEXT represents the floating-point pixel coordinates normalized between 0 and 1
		const vec2 inUV 		= pixelCenter/vec2(gl_LaunchSizeEXT.xy);	//gl_LaunchSizeExt is the image size provided in the traceRayEXT function
		vec2 d 					= inUV * 2.0 - 1.0;

		// Using the pixel coordinates we can apply the inverse transformation of the view and procjection matrices of the camera to obtain
		// the origin and target of the ray
		vec4 origin		= cam.viewInverse * vec4(0,0,0,1);
		vec4 target		= cam.projInverse * vec4(d.x, d.y, 1, 1) ;
		vec4 direction	= cam.viewInverse * vec4(normalize(target.xyz), 0) ;

		float tmin 		= 0.001;
		float tmax 		= 10000.0;

		vec3 rayColor = vec3(1);
		for( int i = 0; i < MAX_RECURSION; i++ )
		{
			// Call the function to start tracing rays.
			traceRayEXT(topLevelAS, gl_RayFlagsOpaqueEXT, 0xff, 0, 0, 0, origin.xyz + direction.xyz * 1e-2, tmin, direction.xyz, tmax, 0);

			rayColor 				*= prd.colorAndDist.xyz;
			const float hitDistance = prd.colorAndDist.w;
			const bool isScattered 	= prd.direction.w > 0;

			if( hitDistance < 0 || !isScattered){
				break;
			}
			else{
				origin.xyz 		= prd.origin;
				direction.xyz 	= prd.direction.xyz;
			}
		}
		finalColor += rayColor;
	}

	finalColor /= NSAMPLES;
	
	// Finally we can write the result to the output image
	if(frame > 0)
	{
		float alpha = 0.3;// = 1.0 / (float(cam.frame) + 1);
		vec3 old_color = imageLoad(image, ivec2(gl_LaunchIDEXT.xy)).xyz;
		vec3 color = mix(old_color, finalColor, alpha);
		imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(color, 0.0));
	}
	else{
		imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(finalColor, 0.0));
	}	
}
